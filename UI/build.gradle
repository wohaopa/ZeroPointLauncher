plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.26.0'
}

group = 'com.github.wohaopa'
version = '0.3.0'

sourceCompatibility = '17'
targetCompatibility = '17'



application {

    applicationDefaultJvmArgs += ["--add-opens", "java.base/java.lang.reflect=com.jfoenix"]
    applicationDefaultJvmArgs += ["--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED"]

    applicationDefaultJvmArgs += ["--add-exports", "javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix"]
    applicationDefaultJvmArgs += ["--add-exports", "javafx.controls/com.sun.javafx.scene.control=com.jfoenix"]
    mainModule = 'com.github.wohaopa.zplui'
    mainClass = 'com.github.wohaopa.zplui.ZplApplication'

}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.base']
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.aliyun.com/nexus/content/groups/public/'
    }
}

dependencies {
    implementation 'com.jfoenix:jfoenix:9.0.10'
    implementation 'cn.hutool:hutool-json:5.8.20'
    implementation project(path: ":Core")

    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'ZeroPointLauncher'
        noConsole = false
    }
    forceMerge('log4j-api')
    jpackage {
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console']
        }
    }
}

jlinkZip {
    group = 'distribution'
}

tasks.register('runZPL', JavaExec) {

    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'boot.Main'
    jvmArgs += ["--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED"]

}
